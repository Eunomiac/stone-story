// =============================================== IMPORTS =====================
var HUD = import HUD
var U = import Utilities
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// =============================================================================
// ================================================ CONFIG =====================
// =============================================================================

// ---- THRESHOLD VALUES ----
var smiteThreshold = 11        // Min number of foes to activate Blade of the Fallen God
var dmgDebuffThreshold = 7     // Total damage debuff on yourself before switching out of low-damage weapons
var splashThreshold = 6        // Total number of foes needed to trigger use of splashing weapons
var foeArmorThreshold = 50     // If foe's armor exceeds this, switch to armor-penetrating weapon
var fastArmorShieldMax = 14    // Max armor of "fast armor"-enchanted shield, if you have one ('fastArmorShield', below)

// ---- ARSENAL ----
// If you don't have a weapon for the indicated slot, set the value to 'false'.

var splashMeleeWeapon = "big aether sword U"    // Name of unmaking splash weapon for mobs
var healthHammer = "vigor hammer"               // Name of rune hammer enchanted with health bonus
var antiArmorMeleeWeapon = "heavy hammer"       // Name of melee weapon to equip vs. strong armor
var debuffDamageWeapon = "poison sword dP"      // Name of weapon to apply debuff_damage (poison)
var debuffDoTWeapon = "fire sword dF"           // Name of weapon to apply debuff_dot (fire)
var debuffChillWeapon = "ice sword dI"          // Name of weapon to apply debuff_chill (ice)                                           
var magicRangedWeapon = "vigor staff"           // Name of magical ranged weapon (able to hit physical-immune enemies)
var lifeStealMeleeWeapon = false                // Name of health-stealing melee weapon if you want to use one
var icePillarWeapon = "big aether sword U"      // Name of weapon to use when facing ice pillars/wall in Icy Ridge
var fastArmorShield = "compound shield +14"     // Name of "fast armor"-enchanted shield
var strongArmorShield = "compound shield +11"   // Name of "high armor"-enchanted shield

// ---- HOTKEY OVERRIDES ----
var twoHandedOverride = "bardiche"              // Two-Handed Weapon to equip when [SHIFT] held down
var oneHandedOverride = false                   // One-Handed Weapon to equip when [SHIFT] held down
    // ** One of the above two should be 'false' -- i.e. pick one or the other, not both **
    
var offhandOverride = strongArmorShield         // Item to equip in right hand when [X] held down

var rangedOverride = "repeating crossbow"       // Ranged Weapon to equip when [CTRL] held down
// *** IMPORTANT *** --- You have to use this with a nonmagical ranged weapon to defeat R.I.Pieces,
//                       the mid-level boss in high-level Halls runs, to shoot through the ghosts it spawns;
//                       otherwise, you'll be overrun.
//                       IF your rangedOverride is a magical weapon, change the following to a non-magical ranged weapon; it
//                       will be chosen instead of rangedOverride while in the Halls location:
var nonMagicRangedOverride = rangedOverride

// ---- DEFAULT EQUIPMENT ----
var mainWeapon = "big aether sword U"           // Name of default main melee weapon
var mainShield = strongArmorShield              // Name of default main shield
var mainRanged = "repeating crossbow"           // Name of default main ranged weapon

var elemWeapons = [  // Names of melee weapons used to take advantage of elemental weaknesses (set to 'mainWeapon' to default to that)
^mainWeapon,           // BASE
^"poison sword D",     // POISON (vs. Vigor)
^"vigor sword D",      // VIGOR (vs. Aether)
^mainWeapon,           // AETHER (vs. Fire)
^"fire sword D",       // FIRE (vs. Ice)
^"ice sword +11 D"     // ICE (vs. Poison)
]

var elemOffHand = [  // Name of second one-handed melee weapon, for double-wielding against elemental bosses. Set to 'false' if no second weapon.
^false,                // BASE
^false,                // POISON (vs. Vigor)
^false,                // VIGOR (vs. Aether)
^false,                // AETHER (vs. Fire)
^false,                // FIRE (vs. Ice)
^"ice sword +10 D"     // ICE (vs. Poison)
] 

var elemRanged = [  // Names of ranged weapons used to take advantage of elemental weaknesses (set to 'mainRanged' to default to that)
^mainRanged,           // BASE
^"poison crossbow D",  // POISON (vs. Vigor)
^"vigor staff D",      // VIGOR (vs. Aether)
^"aether crossbow D",  // AETHER (vs. Fire)
^"fire crossbow D",    // FIRE (vs. Ice)
^"ice crossbow D"      // ICE (vs. Poison)
]

var elemShields = [  // Names of shields used to take advantage of elemental weaknesses (set to 'mainShield' to default to that)
^mainShield,           // BASE
^mainShield,           // POISON (vs. Vigor)
^mainShield,           // VIGOR (vs. Aether)
^mainShield,           // AETHER (vs. Fire)
^mainShield,           // FIRE (vs. Ice)
^mainShield            // ICE (vs. Poison)
]

var locPotions = [ // *U.getHealingPotion: Gets healing potion for which you have the most ingredients, favoring defensive->vampiric->cleansing->healing
^U.getHealingPotion(), // Rocky Plateau 
^U.getHealingPotion(), // Deadwood Canyon
^U.getHealingPotion(), // Caves of Fear
^U.getHealingPotion(), // Mushroom Forest
^U.getHealingPotion(), // Haunted Halls
^"berserk",            // Boiling Mine
^"berserk",            // Icy Ridge
^"berserk"             // Temple
]

var forcedPotions = [ // If one of these potions is brewed at start of run, do not override it
^"Invisibility Potion"
]
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
// _ _ _ _ _ _=== END CONFIG === _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _


// =============================================== WEAPON GETTERS ==============
func getMainWeapon()
  ?splashMeleeWeapon & foe.count > splashThreshold & U.isCloseAs("melee")
    return splashMeleeWeapon    
  ?antiArmorMeleeWeapon & foe.armor > foeArmorThreshold & U.isCloseAs("melee")
    return antiArmorMeleeWeapon
  ?U.getCounterElement()
    return elemWeapons[U.counterElements.IndexOf(U.getCounterElement())]
  return mainWeapon

func getMainOffhandWeapon()
  ?foe.state > 0 & U.getCounterElement() & elemOffHand[U.counterElements.IndexOf(U.getCounterElement())]
    return elemOffHand[U.counterElements.IndexOf(U.getCounterElement())]
  return false

func getMainShield()
  ?armor >= fastArmorShieldMax | fastArmorShield = false
    return strongArmorShield
  return fastArmorShield
  
func getMainRanged()
  ?U.Get("isRangedBlocked")
    return getMainWeapon()
  ?U.getCounterElement() & (foe.hp + foe.armor) > 200
    return elemRanged[U.counterElements.IndexOf(U.getCounterElement())]
  return mainRanged
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================  INIT  =====================
U.Init()
U.Reset()

var locPotion = U.xLookup(loc.name, U.locations, locPotions)
var dmgDebuff = 0
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ TOGGLES ====================
?key = bumpLBegin
  U.Toggle("hideHUD")
:?key = downBegin
  U.Toggle("staffSpeed")
:?key = leftBegin
  U.Toggle("skeletonArm")
:?key = bumpRBegin
  U.Toggle("isRangedBlocked")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ LOCATION START =============
?loc.begin
  U.Reset()
  ?!forcedPotions.Contains(item.potion) & item.potion ! locPotion
    U.brewPotion(locPotion)
:?loc.loop & item.potion ! locPotion
  U.brewPotion(locPotion)
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ TEMPORARY OVERRIDES ========
// --- [SHIFT]: Two-Handed or Main Weapon Override 
?key = ability1Begin
  ?twoHandedOverride
    U.AssignBoth(twoHandedOverride, 4, "** OVERRIDE **")
  :?oneHandedOverride
    U.AssignLeft(oneHandedOverride, 4, "** OVERRIDE **")
?key = ability1End
  U.AssignBoth("", -1, "")
  U.AssignLeft("", -1, "")

// --- [CONTROL]: Ranged Weapon Override
?key = ability2Begin
  ?loc = haunted
    U.AssignLeft(nonMagicRangedOverride, 4, "** OVERRIDE (NM) **")
  :
    U.AssignLeft(getMainRanged(), 4, "** OVERRIDE **")
?key = ability2End
  U.AssignBoth("", -1, "")
  U.AssignLeft("", -1, "")
  
// --- [X]: Offhand Override
?key = backBegin & offhandOverride
  U.AssignRight(offhandOverride, 4, "** OVERRIDE **")
?key = backEnd
  U.AssignRight("", -1, "")    
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ FRAME INITIALIZATION =======
dmgDebuff = U.getDebuffCount("debuff_duration_damage") + U.getDebuffCount("spider_debuff_damage")
U.AssignRight(getMainShield(), 1, "(initial)")
U.AssignLeft(getMainWeapon(), 1, "(initial)")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ HEAL CHECK: LIFESTEAL ======
?hp < maxhp
  ?foe.count = 0 | (foe ! spawner & U.isFarAs("ranged"))
    U.AssignLeft("ouroboros", 1, "Passive Heal")
  :?lifeStealMeleeWeapon & foe.damage <= 3 & foe ! boss
    U.AssignLeft(lifeStealMeleeWeapon, 1, "Lifesteal")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ PICKUP CHECK ===============
?pickup.distance < 13
  U.AssignLeft("star", 2, "Pickup")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ QUARTERSTAFF JUMP CHECK ====
?U.Get("staffSpeed") & item.GetCooldown("quarterstaff") <= 0 & (foe.count = 0 | U.isFarAs("medium")) & item.CanActivate()
  U.AssignBoth("quarterstaff", 20, "@@@ JUMP! @@@")
  U.AssignActivate("Staff")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ RANGED WEAPON CHECK ========
?(item.GetCooldown("blade") > 0 | foe.count <= (smiteThreshold)) & (foe ! boss | (!U.isInState(1, 0) | !ai.idle))
  ?foe ! immune_to_ranged & U.isWithin("close", "ranged") & dmgDebuff <= dmgDebuffThreshold & debuffs.string ! yeti_chill
    U.AssignLeft(getMainRanged(), 2, "Ranged Check")

?foe = magic_vulnerability & U.isCloseAs("ranged")
  U.AssignLeft(magicRangedWeapon, 2, "Magic Weak")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ HARVEST CHECK ==============
?!U.Get("staffSpeed") & harvest.distance < 9
  ?harvest = Boulder
    U.AssignRight("shovel", 2, "Harvest Boulder")
  :
    ?item.GetCooldown("hatchet") <= 0 & harvest.distance <= 4
      U.AssignRight("hatchet", 2, "@@@ HARVEST @@@")
      U.AssignActivate("Hatchet")
    :
      U.AssignRight("hatchet", 2, "Harvest Tree")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================ BARDICHE CHECK =============
?foe ! immune_to_physical & foe.hp > 300 & U.isCloseAs("melee") & item.GetCooldown("bardiche") <= 0
  U.AssignBoth("bardiche", 20, "@@@ BARDICHE @@@")
  U.AssignActivate("Bardiche")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// =========================================== BLADE OF THE FALLEN GOD CHECK ===
?item.GetCooldown("blade") <= 0 & foe.count > smiteThreshold & U.isCloseAs("screen") & item.CanActivate() & foe ! spawner
  U.AssignBoth("blade of the fallen god", 3, "@@@ SMITE @@@")
  U.AssignActivate("Blade")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ================================================= TALISMAN CHECK ============
?summon.count = 0 & screen.i > 0 & item.GetCooldown("fire_talisman") <= 0 & item.CanActivate()
  U.AssignRight("talis", 20, "@@@ SUMMON @@@")
  U.AssignActivate("Talisman")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _



// =============================================================================
// =============================================================================
// ======================[[COMBAT LOGIC LOOP: BOSS FIGHT]]======================
?U.isFacingBoss()

  // --- ［BOSS］ Potion Check, More-Aggressive Heal Check ---  
  ?item.potion = cleansing & debuffs.count > 15
    activate potion
  ?hp < (0.66 * maxhp) & item.potion = defensive & healthHammer
    ?item.left ! healthHammer
      U.AssignLeft(healthHammer, 4, "Hammer Potion")
    :?maxhp > 50
      activate potion
      U.AssignLeft("", -1, "")
  :?hp < (0.5 * maxhp) & (item.potion = healing | item.potion = defensive | item.potion = cleansing | item.potion = vampiric)
    activate potion
    
  // --- [BOSS] Armor Check ---
  ?(foe.armor > 0 & U.isCloseAs("melee"))
    ?item.GetCooldown("hammer") <= 0 & item.CanActivate()
      U.AssignBoth("heavy hammer", 20, "@@@ ARMOR @@@")
      U.AssignActivate("Hammer")
    :
      U.AssignBoth("heavy hammer", 2, "vs. Armor")

  // --- [BOSS] Physical-Immune Check ---
  ?foe = immune_to_physical
    U.AssignLeft(magicRangedWeapon, 3, "Phys-Immune")

  // --- ［BOSS］ Debuff Checks ---
  ?(foe ! pallas | foe ! phase2) & foe.buffs.string ! protection & U.isCloseAs("close")
    ?debuffDamageWeapon & foe.debuffs.string ! damage & foe ! immune_to_debuff_damage
      U.AssignRight("cultist mask", 3, "@-Debuff-@")
      U.AssignLeft(debuffDamageWeapon, 3, "@-Damage-@")
    :?debuffDoTWeapon & foe.debuffs.string ! dot
      U.AssignRight("cultist mask", 3, "@-Debuff-@")
      U.AssignLeft(debuffDoTWeapon, 3, "@-DoT-@")
    :?debuffChillWeapon & U.getFoeDebuffCount("chill") < 6 & foe ! immune_to_debuff_chill
      U.AssignRight("cultist mask", 3, "@-Debuff-@")
      U.AssignLeft(debuffChillWeapon, 3, "@-Chill-" + U.getFoeDebuffCount("chill") + "-@")
    :?foe.maxarmor > 50 & foe.debuffs.string ! fatigue & item.GetCooldown("hammer") <= 0 & item.CanActivate()
      U.AssignBoth("heavy hammer", 20, "@@@ ARMOR @@@")
      U.AssignActivate("Hammer")
    :?foe.debuffs.string ! feeble & item.GetCooldown("mask") <= 0 & item.CanActivate()
      ?foe ! nagaraja
        U.AssignRight("cultist mask", 20, "@@@ ENFEEBLE @@@")
        U.AssignActivate("Mask")

  // --- [BOSS] Offensive Potion Check ---
  ?foe = bronze_guardian & U.isInState(32, 30) & (item.potion = strength | item.potion = berserk)
    activate potion
  ?foe = nagaraja & U.isInState(101, 10) & item.potion = berserk & foe.distance <= 4
    activate potion
  ?foe = bolesh & U.isInState(999, 5) & item.potion = berserk
    activate potion
  ?foe = yeti & U.isInState(32, 1) & item.potion = berserk & U.isCloseAs("close")
    activate potion

  // --- [BOSS] Special Weapon Check ---
  ?foe = bronze_guardian & U.isInState(33, 0) & foe.armor > 0 & item.GetCooldown("hammer") <= 0 & U.isCloseAs("melee")
    U.AssignLeft("heavy hammer", 20, "@@@ ARMOR @@@")
    U.AssignActivate("Hammer")
  ?foe = nagaraja & foe.state > 1
    U.AssignLeft(getMainWeapon(), 2, "!! Naga Left !!")
    U.AssignRight(getMainOffhandWeapon(), 2, "!! Naga Right !!")
  ?foe = ice wall & icePillarWeapon
    U.AssignBoth(icePillarWeapon, 2, "!! Ice Wall !!")
  
  // --- [BOSS] Dodge Check ---
  ?item.GetCooldown("mind") <= 0
    ?foe = bronze_guardian & U.isInState(32, 20)
      U.AssignLeft("mind", 3.5, "<< DODGE <<")
    ?foe = nagaraja & U.isInState(112, 50)
      U.AssignLeft("mind", 3.5, "<< DODGE <<")
    ?foe = dysangelos & foe = phase1 & U.isInState(33, 0)
      U.AssignLeft("mind", 3.5, "<< DODGE <<")
    ?foe = dysangelos & foe = phase3 & U.isInState(115, 60)
      U.AssignLeft("mind", 3.5, "<< DODGE <<")
    ?foe = pallas & foe = phase1 & U.isInState(32, 60)
      U.AssignLeft("mind", 3.5, "<< DODGE <<")
    ?foe = punishment & U.isInState(32, 0)
      U.AssignLeft("mind", 3.5, "<< DODGE <<")
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// =============================================================================
// ======================[[COMBAT LOGIC LOOP: NON-BOSS FIGHT]]==================
:? U.isCloseAs("ranged")
    
  // --- [NON-BOSS] PICK-POCKET CHECK ---
  ?U.Get("skeletonArm") & item.GetCooldown("skeleton_arm") <= 0 & foe ! spawner & foe.hp <= 10 & U.isCloseAs("close") & (item.GetCooldown("blade") > 0 | foe.count <= (smiteThreshold - 4))
    U.AssignBoth("skeleton", 2, "@-Debuff-PPocket-@")
    ?buffs.string = pick_pocket
      ?foe.hp <= 10 & item.CanActivate()
        U.AssignBoth("skeleton", 3, "@@@ PICKPOCKET @@@")
        U.AssignActivate("Skeleton Arm")

  // --- [NON-BOSS] Physical-Immune Check ---
  ?foe = immune_to_physical
    U.AssignLeft(magicRangedWeapon, 3, "Phys-Immune")

  // --- [NON-BOSS] Ice Pillar Check ---
  ?(foe = ice pillar | foe = ice wall) & U.isCloseAs("ranged") & icePillarWeapon
    ?U.canDash()
      U.AssignRight(U.canDash(), 3.5, ">> DASH >>")
    ?U.isCloseAs("close")
      U.AssignLeft(icePillarWeapon, 3, "!! Ice Pillar !!")

  // --- [NON-BOSS (sorta)] Acronian Miniboss Check ---
  ?foe = acronian
    U.AssignLeft(getMainWeapon(), 2, "!! Acro Left !!")
    U.AssignRight(getMainOffhandWeapon(), 2, "!! Acro Right !!")

// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _

// ============================== FINAL OVERRIDING CHECKS ======================
// === DASH CHECK ===
?U.canDash()
  U.AssignRight(U.canDash(), 3.5, ">> DASH >>")
  
// === HEAL CHECK ===
?hp <= (0.25 * maxhp) & (item.potion = healing | item.potion = defensive | item.potion = cleansing | item.potion = vampiric)
  ?item.potion = defensive & healthHammer
    ?item.left ! healthHammer
      U.AssignLeft(healthHammer, 3.4, "Hammer Potion")
    :?maxhp > 50
      activate potion
      U.AssignLeft("", -1, "")
  :
    activate potion

// ======================================= EQUIPPING & ACTIVATION ==============
U.EquipItems()
U.ActivateItem()
HUD.Display()
// _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _ _
